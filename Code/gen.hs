{--
    gen.hs
    Andrew Ribeiro 
    MIT Licence

    Given a subset of some polynomic sequence, produce a function that
    can generate that sequence beyond the subset provided.

    Examples and Background: 

    The root of the sequence of natural numbers [0,1,2,..,+∞] is +1. 
    In this case the root of the sequence is simply the successor function.
    We say that this root is 1 deep, in that one only needs to apply the sequence
    derivitive once before we converge to a constant. 

    In the case of more complex sequences like the sequence of the natural numbers squared,
    [0,1,4,9,16,+∞], the root is 2 deep and requires 2+1 examples of the sequence to converge.
--}
module Gen(deriveSeqLayer,deriveSeqLayer',genSeq,genSeq',funcModel) where
import CommonFn

deriveSeqLayer :: Num a => [a] -> a -> [a]
deriveSeqLayer root rv =  reverse (deriveSeqLayer' (reverse root) rv )

deriveSeqLayer' :: Num a => [a] -> a -> [a]
deriveSeqLayer' [] rv = [rv]
deriveSeqLayer' root rv  = ((head previous)+(head root)):previous
                           where previous = (deriveSeqLayer' (tail' root) rv)

genSeq :: Num a => a -> [a] -> Int -> [a]
genSeq rootVal classVect nTerms = last (genSeq' (take (nTerms-(length classVect)) (cycle [rootVal]) ) classVect)

genSeq' :: Num a => [a] -> [a] -> [[a]]
genSeq' root [] = []
genSeq' root classVect = [layer] ++ ( genSeq' layer (tail' classVect) )
                         where layer = (deriveSeqLayer root (head classVect))

funcModel :: (Num a, Eq a) => [a] -> Int -> [a]
funcModel ls = (\x -> genSeq root classVect x )
               where (root,classVect) = iVect ls

{--
    Def: A polynomic sequence is one that can be generated by mapping some polynomial over the set of natural numbers. 

    We can use funcModel to create a generating function for some polynomic sequence given some example subsequence. 
    If given enough examples in a sequence, it can generalize to the generating function 
    of that sequence. 

    Example: 
    We can use the following command to generate a sequence of numbers constructed by 
    the polymial 2x^4 + 10x^3 + x^2 + 5:

    map (\x -> (2*x^4 + 10*x^3 + x^2 + 5) ) [1,2..20]
    [18,121,446,1173,2530,4793,8286,13381,20498,30105,42718,58901,79266,104473,135230,172293,216466,268601,329598,400405]

    (funcModel [18,121,446,1173,2530]) 20
    [18,121,446,1173,2530,4793,8286,13381,20498,30105,42718,58901,79266,104473,135230,172293,216466,268601,329598,400405]
--}